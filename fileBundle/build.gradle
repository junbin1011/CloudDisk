def isApp = true
if (isApp) {
    //可运行
    apply plugin: 'com.android.application'
} else {
    //作为库
    apply plugin: 'com.android.library'
}

apply plugin: 'com.alibaba.arouter'
apply plugin: 'dagger.hilt.android.plugin'
apply plugin: 'maven'

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        if (isApp) {
            applicationId 'com.cloud.filebundle'
        }
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [AROUTER_MODULE_NAME: project.getName()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError false
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

hilt {
    enableTransformForLocalTests = true
}

group = 'com.cloud.disk.bundle'
archivesBaseName = 'file'
version = '1.0.0'

repositories.mavenCentral()

uploadArchives {
    repositories.mavenDeployer {
        repository(url: 'file:'+ rootDir.getPath()+'/lib')
    }
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation "com.google.dagger:hilt-android:2.28-alpha"
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    annotationProcessor "com.google.dagger:hilt-android-compiler:2.28-alpha"
    implementation 'com.alibaba:arouter-api:1.5.1'
    annotationProcessor 'com.alibaba:arouter-compiler:1.5.1'

    implementation 'com.cloud.disk:api:1.0.0'
    implementation 'com.cloud.disk:library:1.0.0'

    testImplementation 'junit:junit:4.13.2'
    testImplementation "com.google.truth:truth:1.0.1"
    testImplementation 'com.tngtech.archunit:archunit-junit4:0.15.0'
    testImplementation 'org.robolectric:robolectric:4.5.1'
    testImplementation 'androidx.test:core:1.3.0'
    testImplementation 'androidx.test.ext:junit:1.1.2'
    testImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    testImplementation 'androidx.test.espresso:espresso-intents:3.3.0'
    debugImplementation "androidx.fragment:fragment-testing:1.3.1"
    testImplementation 'com.google.dagger:hilt-android-testing:2.28-alpha'
    testAnnotationProcessor 'com.google.dagger:hilt-android-compiler:2.28-alpha'
}